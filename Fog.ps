#define NUM_LIGHTS 4
#define NUM_POISSON_SAMPLES 4

Texture2D shaderTexture1 : register(t0);
Texture2D shaderTexture2 : register(t1);
Texture2D alphaMap : register(t2);
Texture2D normalMap : register(t3);
Texture2D shadowMap : register(t4);
SamplerState SampleType : register(s0);

cbuffer LightColorBuffer : register(b1)
{
    float4 diffuseColor[NUM_LIGHTS];
};

cbuffer UtilBuffer: register(b0)
{
    float time;
    float pad2;
    float pad3;
    float pad4;
};

struct PixelInputType
{
    float4 position : SV_POSITION;
    float4 worldPos : POSITION1;

    float2 tex : TEXCOORD0;
    float3 normal : NORMAL;
    float3 tangent : TANGENT;
    float3 binormal : BINORMAL;
    float3 lightPos[NUM_LIGHTS] : TEXCOORD2;
    float3 viewDirection : TEXCOORD1;
    float fogFactor : FOG;
    float clip : SV_ClipDistance0;
};

cbuffer LightBuffer : register(b2)
{
    float4 ambientColor;
    float4 diffuseDirColor;
    float3 lightDirection;
    float specularPower;
    float4 specularColor;
};

cbuffer TextureTranslationBuffer : register(b3)
{
    float2 translation;
    float timeMultiplier;
    float texPad;
};

cbuffer AlphaBuffer : register(b4)
{
    float alphaBlend;
    float alphaPad1;
    float alphaPad2;
    float alphaPad3;
};

cbuffer ShadowBuffer : register(b5)
{
    matrix lightViewMatrix;
    matrix lightProjectionMatrix;

    float usingShadows;
    float sPad1;
    float sPad2;
    float sPad3;

    float2 poissonDisk[NUM_POISSON_SAMPLES];
};

float4 PS_MAIN(PixelInputType input) : SV_TARGET
{
    float4 texCol1, texCol2, blendCol, alphaMapCol;
    float lightIntensity[NUM_LIGHTS];
    float4 colorArray[NUM_LIGHTS];
    float3 reflection;
    float4 specular;
    float4 bumpMap;
    float3 bumpNormal;
    float shadowAverage = 1;

    input.tex.xy += time * timeMultiplier;
    input.tex += translation;

    float4 colorSum = ambientColor;

    texCol1 = shaderTexture1.Sample(SampleType, input.tex);
    texCol2 = shaderTexture2.Sample(SampleType, input.tex);
    alphaMapCol = alphaMap.Sample(SampleType, input.tex);

    // If tex2 is not assigned then texCol2 == texCol1
    blendCol = (alphaMapCol * texCol1) + ((1.0 - alphaMapCol) * texCol2);
    blendCol = saturate(blendCol);

    bumpMap = normalMap.Sample(SampleType, input.tex);
    bumpMap = (bumpMap * 2.0f) - 1.0f;

    bumpNormal = (bumpMap.x * input.tangent) + (bumpMap.y * input.binormal) + (bumpMap.z * input.normal);
    bumpNormal = normalize(bumpNormal);

    for(int i=0; i<NUM_LIGHTS; i++)
    {
        lightIntensity[i] = saturate(dot(bumpNormal, input.lightPos[i]));
        colorArray[i] = diffuseColor[i] * lightIntensity[i];
        colorSum.rgb += colorArray[i].rgb * colorArray[i].a;
    }

    float intensity = saturate(dot(bumpNormal, -lightDirection));

    if (usingShadows)
    {
        float4 lightSpacePos = mul(mul(input.worldPos, lightViewMatrix), lightProjectionMatrix);
        lightSpacePos.xyz /= lightSpacePos.w;
        lightSpacePos = lightSpacePos * 0.5f + 0.5f;

        lightSpacePos.y = 1 - lightSpacePos.y;      
        
        shadowAverage = 0;

        for (int i = 0; i < NUM_POISSON_SAMPLES; i++)
        {
            float2 offset = poissonDisk[i] / 2048;
            float shadowDepth = shadowMap.Sample(SampleType, lightSpacePos.xy + offset).r;  
            shadowAverage += lightSpacePos.z - 0.005f < shadowDepth;
        }      

        shadowAverage /= NUM_POISSON_SAMPLES;

        intensity *= shadowAverage;
    }

    if(intensity > 0.0f && shadowAverage > 0.6f)
    {        
        colorSum += diffuseDirColor * intensity;
        reflection = normalize(2.0f * intensity * bumpNormal + lightDirection); 
        specular = pow(saturate(dot(reflection, input.viewDirection)), specularPower);
    }

    float4 finalColor = saturate(saturate(colorSum + specular) * blendCol);
    finalColor.a = alphaBlend;

    float4 fogColor = float4(0.5f, 0.5f, 0.5f, 1.0f);

    return input.fogFactor * finalColor + (1.0f - input.fogFactor) * fogColor;
}